// function for getting data by URL with navigation
(url as text) as table =>
    let
        // import required functions
        GetStructuredData = Extension.LoadFunction("GetStructuredData.pqm"),
        GetRawJSON = Extension.LoadFunction("GetRawJSON.pqm"),
        GetModel = Extension.LoadFunction("GetModel.pqm"),

        // the logic for importing functions from other files
        Extension.LoadFunction = (fileName as text) =>
            let
                binary = Extension.Contents(fileName),
                asText = Text.FromBinary(binary)
            in
                try
                    Expression.Evaluate(asText, #shared)
                catch (e) =>
                    error
                        [
                            Reason = "Extension.LoadFunction Failure",
                            Message.Format = "Loading '#{0}' failed - '#{1}': '#{2}'",
                            Message.Parameters = {fileName, e[Reason], e[Message]},
                            Detail = [File = fileName, Error = e]
                        ],

        // get model info to access the model name
        modelInfo = GetModel(url),
        modelName = modelInfo[modelName],
        
        // create navigation table from raw and structured data
        source = #table(
            {"Name", "Data"},
            {
                { "Structured Data - " & modelName, GetStructuredData(url)},
                { "Viewer Data - " & modelName, GetRawJSON(url)}
            }
        ),
        
        // add navigation table metadata directly
        tableType = Value.Type(source),
        newTableType = Type.AddTableKey(tableType, {"Name"}, true) meta [
            NavigationTable.NameColumn = "Name",
            NavigationTable.DataColumn = "Data",
            Documentation.Name = "Speckle Model"
        ],
        
        // convert to navigation table
        navTable = Value.ReplaceType(source, newTableType)
    in
        navTable