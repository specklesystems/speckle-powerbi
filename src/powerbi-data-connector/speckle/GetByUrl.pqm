(url as text) as table =>
    let
        // import required functions
        GetModel = Extension.LoadFunction("GetModel.pqm"),
        Parser = Extension.LoadFunction("Parser.pqm"),
        GetStructuredData = Extension.LoadFunction("GetStructuredData.pqm"),

        // the logic for importing functions from other files
        Extension.LoadFunction = (fileName as text) =>
            let
                binary = Extension.Contents(fileName),
                asText = Text.FromBinary(binary)
            in
                try
                    Expression.Evaluate(asText, #shared)
                catch (e) =>
                    error
                        [
                            Reason = "Extension.LoadFunction Failure",
                            Message.Format = "Loading '#{0}' failed - '#{1}': '#{2}'",
                            Message.Parameters = {fileName, e[Reason], e[Message]},
                            Detail = [File = fileName, Error = e]
                        ],

        // get model info and parsed URL
        modelInfo = GetModel(url),
        modelName = modelInfo[modelName],
        rootId = modelInfo[rootObjectId],
        parsedUrl = Parser(url),
        
        // get API key if available
        apiKey = try Extension.CurrentCredential()[Key] otherwise null,
        
        // make the API request to objects endpoint
        Source = Web.Contents(
            Text.Combine({parsedUrl[baseUrl], "objects", parsedUrl[projectId], modelInfo[rootObjectId]}, "/"),
            [
                Headers = [
                    #"Authorization" = if apiKey = null then "" else Text.Format("Bearer #{0}", {apiKey})
                ],
                ManualStatusHandling = {400, 401, 403}
            ]
        ),
        
        // parse the JSON response
        JsonResponse = Json.Document(Source),
        JsonText = Json.FromValue(JsonResponse),
        
        // Send to localhost
        SendOperation = Web.Contents(
            "http://127.0.0.1:8099/send-data",
            [
                Headers = [
                    #"Content-Type" = "application/json",
                    #"Method" = "POST"
                ],
                Content = JsonText,
                ManualStatusHandling = {400, 401, 403, 404, 500}
            ]
        ),

        // Get sending status
        SendStatus = if Value.Metadata(SendOperation)[Response.Status] = 200 
            then "Data successfully sent to localhost" 
            else "Failed to send data",
        
        // get structured data
        StructuredData = GetStructuredData(url),

        // get viewer data
        ViewerData = #table(
            {"Root Object ID"},
            {{rootId}}
        ),
        
        // create navigation table
        source = #table(
            {"Name", "Data"},
            {
                { 
                    "Structured Data - " & modelName, 
                    StructuredData
                },
                {
                    "Viewer Data",
                    ViewerData
                }
            }
        ),
        
        // add navigation table metadata
        tableType = Value.Type(source),
        newTableType = Type.AddTableKey(tableType, {"Name"}, true) meta [
            NavigationTable.NameColumn = "Name",
            NavigationTable.DataColumn = "Data",
            NavigationTable.DescriptionColumn = "Description",
            Documentation.Name = "Speckle Model"
        ],
        
        // convert to navigation table
        navTable = Value.ReplaceType(source, newTableType)
    in
        navTable