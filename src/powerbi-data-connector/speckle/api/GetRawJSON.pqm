// function for getting JSON data with prefixed chunks
(url as text) as table =>
    let
        // import the Parser and GetModel functions
        Parser = Extension.LoadFunction("Parser.pqm"),
        GetModel = Extension.LoadFunction("GetModel.pqm"),
        Extension.LoadFunction = (fileName as text) =>
            let
                binary = Extension.Contents(fileName),
                asText = Text.FromBinary(binary)
            in
                try
                    Expression.Evaluate(asText, #shared)
                catch (e) =>
                    error
                        [
                            Reason = "Extension.LoadFunction Failure",
                            Message.Format = "Loading '#{0}' failed - '#{1}': '#{2}'",
                            Message.Parameters = {fileName, e[Reason], e[Message]},
                            Detail = [File = fileName, Error = e]
                        ],

        // get parsed URL components and model info
        parsedUrl = Parser(url),
        server = parsedUrl[baseUrl],
        modelInfo = GetModel(url),
        
        // get API key if available
        apiKey = try Extension.CurrentCredential()[Key] otherwise null,
        
        // make the API request to objects endpoint
        Source = Web.Contents(
            Text.Combine({server, "objects", parsedUrl[projectId], modelInfo[rootObjectId]}, "/"),
            [
                Headers = [
                    #"Authorization" = if apiKey = null then "" else Text.Format("Bearer #{0}", {apiKey})
                ],
                Query = [timestamp = Text.From(DateTime.LocalNow())],
                ManualStatusHandling = {400, 401, 403}
            ]
        ),
        
        // convert to JSON string
        JsonString = Text.FromBinary(Json.FromValue(Json.Document(Source))),
        TotalLength = Text.Length(JsonString),
        
        // calculate number of chunks needed
        ChunkSize = 29900,
        NumChunks = Number.RoundUp(TotalLength/ChunkSize),
        
        // generate list of chunk indices
        ChunkIndices = List.Numbers(0, NumChunks),
        
        // function to format row number with appropriate padding
        FormatRowNumber = (number as number) as text =>
            let
                digits = Number.RoundUp(Number.Log10(number + 1)),
                padLength = if digits < 7 then 7 else digits,
                formattedNumber = Text.PadStart(Text.From(number), padLength, "0")
            in
                formattedNumber,
        
        // create chunks with formatted prefix
        CreateChunk = (index) =>
            let
                Start = index * ChunkSize,
                Length = if Start + ChunkSize > TotalLength 
                        then TotalLength - Start 
                        else ChunkSize,
                RawChunk = Text.Range(JsonString, Start, Length),
                // format row number with dynamic padding
                FormattedIndex = FormatRowNumber(index + 1),
                PrefixedChunk = "R_" & FormattedIndex & RawChunk
            in
                [
                    viewer_data = PrefixedChunk
                ],
        
        // convert to table
        Chunks = Table.FromRecords(List.Transform(ChunkIndices, CreateChunk)),

        Response = Web.Contents(
            "http://localhost:8091/send-data",
            [
                Headers = [#"Content-Type" = "application/json"],
                Query = [timestamp = Text.From(DateTime.LocalNow())],
                Content = Json.FromValue([key = 235.7, value = 41.53])
            ]
        ),
        JsonResponse = Json.Document(Response),
        
        // set correct data types
        FinalTable = Table.TransformColumnTypes(
            Chunks,
            {
                {"viewer_data", type text}
            }
        )        
    in
        FinalTable