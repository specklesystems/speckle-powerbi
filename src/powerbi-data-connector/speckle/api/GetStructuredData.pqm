// function for getting structured object data
(url as text) as table =>
    let
        // import the required functions
        GetModel = Extension.LoadFunction("GetModel.pqm"),
        SendToServer = Extension.LoadFunction("SendToServer.pqm"),
       
        // the logic for importing functions from other files
        Extension.LoadFunction = (fileName as text) =>
            let
                binary = Extension.Contents(fileName),
                asText = Text.FromBinary(binary)
            in
                try
                    Expression.Evaluate(asText, #shared)
                catch (e) =>
                    error
                        [
                            Reason = "Extension.LoadFunction Failure",
                            Message.Format = "Loading '#{0}' failed - '#{1}': '#{2}'",
                            Message.Parameters = {fileName, e[Reason], e[Message]},
                            Detail = [File = fileName, Error = e]
                        ],
       
        // get model info and server data
        modelInfo = GetModel(url),
        rootId = modelInfo[rootObjectId],
       
        // Get the data from SendToServer - this is already a response from the service
        JsonResponse = SendToServer(url),
       
        // convert list to table with all columns expanded
        TableFromList = Table.FromList(
            JsonResponse,
            Splitter.SplitByNothing(),
            null,
            null,
            ExtraValues.Error
        ),
        // fields to remove from data record
        FieldsToRemove = {"__closure", "totalChildrenCount", "renderMaterialProxies"},
        // create the final table with cleaned data records
        FinalTable = Table.FromRecords(
            List.Transform(
                TableFromList[Column1],
                each let
                    record = _,
                    fieldsToRemoveForThisRecord = List.Select(
                        FieldsToRemove,
                        each Record.HasFields(record, {_})
                    )
                in
                    [
                        #"Object IDs" = record[id], // Object IDs
                        #"Speckle Type" = record[speckle_type], // Speckle Type
                        #"Version Object ID" = rootId,
                        data = Record.RemoveFields(record, fieldsToRemoveForThisRecord) // Data
                    ]
            )
        ),
        
        // helper function to extract referenced IDs from displayValue fields
        ExtractDisplayValueIds = (dataRecord as record) as list =>
            let
                displayValue = Record.FieldOrDefault(dataRecord, "displayValue", null)
            in
                if displayValue = null then
                    {}
                else if Value.Is(displayValue, type list) then
                    // handle array of displayValue references
                    List.Transform(
                        displayValue,
                        each if Value.Is(_, type record) and Record.HasFields(_, {"referencedId"}) then
                            _[referencedId]
                        else
                            null
                    )
                else if Value.Is(displayValue, type record) and Record.HasFields(displayValue, {"referencedId"}) then
                    // Handle single displayValue reference
                    {displayValue[referencedId]}
                else
                    {},
        
        // collect all object IDs that are referenced as displayValues
        DisplayValueIds = List.Distinct(
            List.Combine(
                List.Transform(
                    Table.Column(FinalTable, "data"),
                    ExtractDisplayValueIds
                )
            )
        ),
       
        // function to check if a row should be excluded based on speckle type
        ShouldExcludeRow = (row as record) as logical =>
            let
                speckleType = Record.FieldOrDefault(row[data], "speckle_type", ""),
                objectId = row[#"Object IDs"]
            in
                speckleType = "Speckle.Core.Models.DataChunk" or 
                Text.Contains(speckleType, "Objects.Other.RawEncoding") or
                List.Contains(DisplayValueIds, objectId), // Exclude objects referenced as displayValue
       
        // Filtering logic here 
        // If model data contains any DataObject -> fetch only data objects (excluding unwanted types)
        // If there are no data objects in the data -> fetch everything but exclude DataChunks and RawEncoding
        HasDataObjects = Table.RowCount(
            Table.SelectRows(
                FinalTable, 
                each Text.Contains(Record.FieldOrDefault([data], "speckle_type", ""), "DataObject") 
                     and not ShouldExcludeRow(_)
            )
        ) > 0,

        FilteredTable = if HasDataObjects then
            Table.SelectRows(
                FinalTable, 
                each Text.Contains(Record.FieldOrDefault([data], "speckle_type", ""), "DataObject") 
                     and not ShouldExcludeRow(_)
            )
        else
            Table.SelectRows(FinalTable, each not ShouldExcludeRow(_))
    in
        FilteredTable