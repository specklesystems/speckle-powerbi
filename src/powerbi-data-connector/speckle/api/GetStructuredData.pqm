// function for getting structured object data
(url as text) as table =>
    let
        // import the required functions
        GetModel = Extension.LoadFunction("GetModel.pqm"),
        SendToServer = Extension.LoadFunction("SendToServer.pqm"),
       
        // the logic for importing functions from other files
        Extension.LoadFunction = (fileName as text) =>
            let
                binary = Extension.Contents(fileName),
                asText = Text.FromBinary(binary)
            in
                try
                    Expression.Evaluate(asText, #shared)
                catch (e) =>
                    error
                        [
                            Reason = "Extension.LoadFunction Failure",
                            Message.Format = "Loading '#{0}' failed - '#{1}': '#{2}'",
                            Message.Parameters = {fileName, e[Reason], e[Message]},
                            Detail = [File = fileName, Error = e]
                        ],
       
        // get model info and server data
        modelInfo = GetModel(url),
        rootId = modelInfo[rootObjectId],
       
        // Get the data from SendToServer - this is already a response from the service
        JsonResponse = SendToServer(url),
       
        // convert list to table with all columns expanded
        TableFromList = Table.FromList(
            JsonResponse,
            Splitter.SplitByNothing(),
            null,
            null,
            ExtraValues.Error
        ),
        // fields to remove from data record
        FieldsToRemove = {"__closure", "totalChildrenCount", "renderMaterialProxies"},
        // create the final table with cleaned data records
        FinalTable = Table.FromRecords(
            List.Transform(
                TableFromList[Column1],
                each let
                    record = _,
                    fieldsToRemoveForThisRecord = List.Select(
                        FieldsToRemove,
                        each Record.HasFields(record, {_})
                    )
                in
                    [
                        #"Object IDs" = record[id], // Object IDs
                        #"Speckle Type" = record[speckle_type], // Speckle Type
                        #"Version Object ID" = rootId,
                        data = Record.RemoveFields(record, fieldsToRemoveForThisRecord) // Data
                    ]
            )
        ),
       
        // Check if data contains any DataObject
        HasDataObject = Table.RowCount(
            Table.SelectRows(FinalTable, each 
                Text.Contains(Record.FieldOrDefault([data], "speckle_type", ""), "DataObject") or
                Text.Contains(Record.FieldOrDefault([data], "speckle_type", ""), "Objects.Data"))
        ) > 0,

        // Apply the filtering strategy
        FilteredTable = if HasDataObject then
            // If DataObjects exists, keep only those data objects
            Table.SelectRows(FinalTable, each 
                Text.Contains(Record.FieldOrDefault([data], "speckle_type", ""), "DataObject") or
                Text.Contains(Record.FieldOrDefault([data], "speckle_type", ""), "Objects.Data"))
        else
            // Otherwise, keep all but DataChunks
            Table.SelectRows(FinalTable, each Record.FieldOrDefault([data], "speckle_type", "") <> "Speckle.Core.Models.DataChunk")
    in
        FilteredTable