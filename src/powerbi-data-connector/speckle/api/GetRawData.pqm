// function for getting object data
(url as text) as table =>
    let
        // import the Parser and GetModel functions
        Parser = Extension.LoadFunction("Parser.pqm"),
        GetModel = Extension.LoadFunction("GetModel.pqm"),
        // the logic for importing functions from other files
        Extension.LoadFunction = (fileName as text) =>
            let
                binary = Extension.Contents(fileName),
                asText = Text.FromBinary(binary)
            in
                try
                    Expression.Evaluate(asText, #shared)
                catch (e) =>
                    error
                        [
                            Reason = "Extension.LoadFunction Failure",
                            Message.Format = "Loading '#{0}' failed - '#{1}': '#{2}'",
                            Message.Parameters = {fileName, e[Reason], e[Message]},
                            Detail = [File = fileName, Error = e]
                        ],
    
        // get parsed URL components and model info
        parsedUrl = Parser(url),
        server = parsedUrl[baseUrl],
        modelInfo = GetModel(url),
        
        // get API key if available
        apiKey = try Extension.CurrentCredential()[Key] otherwise null,
        
        // make the API request to objects endpoint
        Source = Web.Contents(
            Text.Combine({server, "objects", parsedUrl[projectId], modelInfo[rootObjectId]}, "/"),
            [
                Headers = [
                    #"Authorization" = if apiKey = null then "" else Text.Format("Bearer #{0}", {apiKey})
                ],
                ManualStatusHandling = {400, 401, 403}
            ]
        ),
        
        // parse the response and return the raw JSON
        JsonResponse = Json.Document(Source),

        ConvertedToTable = Table.FromList(
            JsonResponse, 
            Splitter.SplitByNothing(),
            {"viewer_data"},
            null,
            ExtraValues.Error
        ),

        // expand the table with speckle_id column to set the relationship between raw and structured data
        // not sure if it is necesarry, will test it once we have the visual up and running
        ExpandedTable = Table.AddColumn(
            ConvertedToTable, 
            "speckle_id", 
            each Record.Field([viewer_data], "id"), 
            type text
        )
    in
        ExpandedTable