// function for federating multiple tables by combining them and creating a concatenated Metadata field
(tables as list, optional excludeData as logical) as table =>
let
    ViewerOnly = if excludeData = null then false else excludeData,

    // filter columns from each table if excludeData is true
    ProcessedTables = List.Transform(
        tables,
        each
            if ViewerOnly then
                Table.SelectColumns(_, {"Metadata", "Object IDs"}, MissingField.Ignore)
            else
                _
    ),

    CombinedTable = Table.Combine(ProcessedTables),

    DistinctMetadata = List.Distinct(CombinedTable[Metadata]),
    ConcatenatedMetadata = Text.Combine(DistinctMetadata, ","),

    // Replace all Metadata values with the concatenated string
    FederatedTable = Table.ReplaceValue(
        CombinedTable,
        each [Metadata],
        ConcatenatedMetadata,
        Replacer.ReplaceText,
        {"Metadata"}
    )
in
    FederatedTable