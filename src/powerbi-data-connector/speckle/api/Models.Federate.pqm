// function for federating multiple tables by combining them and creating a concatenated Model Info field
(tables as list, optional excludeData as logical) as table =>
let
    ViewerOnly = if excludeData = null then false else excludeData,

    // filter columns from each table if excludeData is true
    ProcessedTables = List.Transform(
        tables,
        each
            if ViewerOnly then
                Table.SelectColumns(_, {"Model Info", "Object IDs"}, MissingField.Ignore)
            else
                _
    ),

    CombinedTable = Table.Combine(ProcessedTables),

    DistinctModelInfo = List.Distinct(CombinedTable[Model Info]),
    ConcatenatedModelInfo = Text.Combine(DistinctModelInfo, ","),

    // Replace all Metadata values with the concatenated string
    FederatedTable = Table.ReplaceValue(
        CombinedTable,
        each [Model Info],
        ConcatenatedModelInfo,
        Replacer.ReplaceText,
        {"Model Info"}
    )
in
    FederatedTable