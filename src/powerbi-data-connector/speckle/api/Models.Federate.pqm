// function for federating multiple tables by combining them and creating concatenated Version Object ID and Model Info fields
(tables as list, optional excludeData as logical) as table =>
let
    ViewerOnly = if excludeData = null then false else excludeData,

    // filter columns from each table if excludeData is true
    ProcessedTables = List.Transform(
        tables,
        each
            if ViewerOnly then
                Table.SelectColumns(_, {"Version Object ID", "Model Info", "Object IDs"}, MissingField.Ignore)
            else
                _
    ),

    CombinedTable = Table.Combine(ProcessedTables),

    DistinctVersionObjectIDs = List.Distinct(CombinedTable[Version Object ID]),
    ConcatenatedVersionObjectIDs = Text.Combine(DistinctVersionObjectIDs, ","),

    DistinctModelInfo = List.Distinct(CombinedTable[Model Info]),
    ConcatenatedModelInfo = Text.Combine(DistinctModelInfo, ","),

    // Replace all Version Object ID values with the concatenated string
    TableWithVersionObjectID = Table.ReplaceValue(
        CombinedTable,
        each [Version Object ID],
        ConcatenatedVersionObjectIDs,
        Replacer.ReplaceText,
        {"Version Object ID"}
    ),

    // Replace all Model Info values with the concatenated string
    FederatedTable = Table.ReplaceValue(
        TableWithVersionObjectID,
        each [Model Info],
        ConcatenatedModelInfo,
        Replacer.ReplaceText,
        {"Model Info"}
    )
in
    FederatedTable