(url as text) as record =>
    let
        ApiFetch = Extension.LoadFunction("Api.Fetch.pqm"),
        Parser = Extension.LoadFunction("Parser.pqm"),
        
        // the logic for importing functions from other files
        Extension.LoadFunction = (fileName as text) =>
            let
                binary = Extension.Contents(fileName),
                asText = Text.FromBinary(binary)
            in
                try
                    Expression.Evaluate(asText, #shared)
                catch (e) =>
                    error
                        [
                            Reason = "Extension.LoadFunction Failure",
                            Message.Format = "Loading '#{0}' failed - '#{1}': '#{2}'",
                            Message.Parameters = {fileName, e[Reason], e[Message]},
                            Detail = [File = fileName, Error = e]
                        ],
        
        // parse the URL to extract project id
        parsedUrl = Parser(url),
        server = parsedUrl[baseUrl],
        projectId = parsedUrl[projectId],
        
        // first GraphQL query to get workspace ID from project
        projectQuery = "query Project($projectId: String!) {
            data:project(id: $projectId) {
                workspaceId
            }
        }",
        
        projectVariables = [
            projectId = projectId
        ],
        
        projectResult = ApiFetch(server, projectQuery, projectVariables),
        
        // check that the project result contains the expected structure
        projectStructureCheck = if not (Record.HasFields(projectResult, {"data"}) and 
                  Record.HasFields(projectResult[data], {"workspaceId"})) then
            error "Invalid response structure from project query"
        else
            null,
        
        workspaceId = projectResult[data][workspaceId],
        
        // query to get workspace details
        workspaceQuery = "query Workspace($workspaceId: String!) {
            data:workspace(id: $workspaceId) {
                logo
                name
                embedOptions {
                    hideSpeckleBranding
                }
            }
        }",
        
        workspaceVariables = [
            workspaceId = workspaceId
        ],
        
        workspaceResult = ApiFetch(server, workspaceQuery, workspaceVariables),
        
        // check that the workspace result contains the expected structure
        workspaceStructureCheck = if not (Record.HasFields(workspaceResult, {"data"}) and 
                  Record.HasFields(workspaceResult[data], {"logo", "name", "embedOptions"}) and
                  Record.HasFields(workspaceResult[data][embedOptions], {"hideSpeckleBranding"})) then
            error "Invalid response structure from workspace query"
        else
            null,
        
        workspace = workspaceResult[data],
        
        workspaceInfo = [
            workspaceId = workspaceId,
            workspaceLogo = workspace[logo],
            workspaceName = workspace[name],
            hideSpeckleBranding = workspace[embedOptions][hideSpeckleBranding]
        ]
    in
        workspaceInfo